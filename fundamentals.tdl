; -*- Mode: TDL; Coding: utf-8 -*-

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;        file: fundamentals.tdl
;;;  written by: Melanie Siegel
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; author            | date        | modification
;;; -----------------|--------- ---|------------------------------------------
;;;Melanie Siegel(MS)|             | Emily Bender (ERB), Francis Bond (FCB), 
;;;                  |             | Sanae Fujita (SF), Takaaki Tanaka (TT),
;;;                  |             | Akira Ohtani (AO), Chikara Hashimoto (CH)
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;; specify this a bit further
valence :+ 
 [UNSAT bool,
  SUBJ 0-1-list,
  SPR 0-1-list].


formal := avm & 
          [SHON bool,
           AHON bool].

;(MS 2003-07-08) replaced MOD mod_synsem with MOD 0-1-list, in order to adapt to Matrix standards.

head :+ 
 [POS category,
  MARK 0-1-list,
  MOD 0-1-list,
  C-MOD 0-1-list,
  J-SPEC 0-1-list,
  FORMAL formal,
  J-POSTHEAD posthead,
  SMOD smod,
  QUANTIFIABLE bool,
  H-TENSE tense,
  FIN bool,
  AUX aux,
  COP bool,
  VN bool,
  ADJ bool,
  LIGHT bool,
  EMPTY bool,
  MAIN-PRD bool,
  COP-ARG bool,
  MODUS mood].

c-indices := avm & 
             [SPEAKER index,
              ADDRESSEE index].

honor_rel := avm &
             [HONORER index,
              HONORED index,
              POLARITY bool,
              BEARER individual].

subj-honor_rel :< honor_rel.

obj-honor_rel :< honor_rel.

addr-honor_rel :< honor_rel.

;;; More details for context
; indices, background and empathy
ctxt :+ 
 [C-INDICES c-indices & [SPEAKER #sp],
  BACKGROUND diff-list & [LIST list],
  EMPATHY empathy_rel & [EMPER #sp]].


empathy_rel := avm &
               [EMPER index,
                EMPEE index].

cat :+ [HEAD head, VAL valence].

; (ERB 2002-01-24) Change value of NUCL from bool to nucl in order
; to be able to keep phrases built by conj-rule from being modified.
; FCB 

local :+ [CAT cat,
	  CTXT ctxt,
	  NUCL nucl,
	  BAR bool].



; (ERB 2001-11-28) Types to distinguish between intersective
; and scopal modifiers (will be used in their MOD values). 
; (Borrowed from LinGO grammar.)

; (MS 2003-07-10) I cannot use the definitions in matrix.tdl, because they are subtypes of local and I need them to be subtypes of j-local.

scopal_mod := local.
intersective_mod := local.

;;; Do we use these additions?
 non-local :+ 
  [ROBUST bool,
   UNDERSPEC_ARG bool,
   SLASH 0-dlist,
   AFFIX diff-list].

;;; Redefine these (unused in JACY as we have no slash)
extracted-adj-phrase := basic-extracted-adj-phrase.
basic-head-filler-phrase := binary-phrase & phrasal.
gap := expressed-non-canonical.

;;;;;;;;;(MS 2002-11-12) Matrix approach to subcategorization;;;;;;;;;;;;;;


2-comps-list := 1-plus-list &
		< synsem, synsem >.

saturated := optional-or-saturated & [SUBJ null,
			COMPS null,
			SPR null].

;sat-or-opt-arg := olist.

;sat-arg := sat-or-opt-arg & onull.

opt-1-arg := olist & 1-list & [FIRST.OPT + ].

opt-2-args := olist & 2-comps-list & [FIRST.OPT + ,
					       REST.FIRST.OPT + ].

obl-arg := cons.
obl-1-arg := obl-arg & 1-list & [FIRST.OPT - ].
obl-2-args := obl-arg & 2-comps-list & [FIRST.OPT - ,
					REST.FIRST.OPT - ].


optional-or-saturated := valence.

only-optional-arguments := optional-or-saturated & [SUBJ olist,
						    COMPS olist,
						    SPR olist].


canonical-synsem :+ 
 [ MODIFIED.PERIPH bool].


opt_synsem := canonical-synsem & [OPT + ].
obl_synsem := canonical-synsem & [OPT - ].

; (MS 2003-07-09) changed due to the sign definition in matrix.tdl
; Idiom Implementation (CH 060804)

sign :+
  [ IDIOM bool,
    LMORPH-BIND-TYPE morphbind,
    RMORPH-BIND-TYPE morphbind,
    J-NEEDS-AFFIX bool].
   	

; So, I'm going to take the constraint off of lexical_sign, make
; three subtypes of lexical_sign (lexical_sign-word, lexical_sign-affix,
; and lexical_sign-rule) and make the constraint live on the first 
; of these.
lexical_sign := word & 
               [SYNSEM lex-synsem & [LOCAL [CAT.VAL.UNSAT +],
				       LEX +],
                STEMTYPE stemtype].

;;; Most words should share their lable
lexical_sign-word := lexical_sign &
 [ SYNSEM [LOCAL.CONT.HOOK.LTOP #label,
	   LKEYS.KEYREL [ LBL #label]]].

;;; abnormal  words don't
lexical_sign-aword := lexical_sign.


lexical_sign-affix := lexical_sign.

;;;
;;; Idiom Implementation (CH 060804)
;;;
lexical_sign-rule := lexical_sign & phrase-or-lexrule &
 [IDIOM #idiom,
  ARGS <[IDIOM #idiom]>].

;lexical_sign-rule := lexical_sign & phrase-or-lexrule.



wg-handel-lex-mrs-basic :< mrs.

; (ERB 2001-11-26) Not all lexical sem types inherit the constraints
; that MOD-IND = INDEX and MOD-HAND = LTOP.  I'm going to move these
; constraints out of this type.

; (MS 2003-07-09) This has to be changed due to the matrix!!!

wg-handel-lex-mrs := wg-handel-lex-mrs-basic.


mod-id-mrs := mrs.

mod-id-wg-handel-mrs := mod-id-mrs & wg-handel-lex-mrs.

wg-handel-id := relation &
                [ID *top*,
                 LBLS list,
                 WORD string].



; (MS 2004-11-18) We want some hint about passivization in the MRS (and RMRS)!

tam :+ 
  [ PASS bool ]. 


never_unify_rel := relation.

;;; (FCB ) added an ARG0 to messages - harmonizing with ERG 

; The ARG0 is the highest scoping event in the relation that 
; the MARG qeqs.

message :+ arg0-relation.

; For fragments (DPF FCB 2005-08-22) expect to appear in the Matrix any moment now
unknown_relation := arg0-relation &
  [ PRED unknown_rel,
    ARG0 event,
    ARG semarg ].

unknown_rel := predsort.


