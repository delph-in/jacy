;;; -*- Mode: TDL; Coding: utf-8 -*-
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; author            | date        | modification
;;; ------------------|-------------|------------------------------------------
;;;Melanie Siegel (MS)|             | Emily Bender (ERB), Francis Bond (FCB),
;;;                   |             | Sanae Fujita (SF), Chikara Hashimoto (CH)
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

%(letter-set(!v べけめせでれねえげてびきみしいぎりじにぜち))
%(letter-set(!h あうえおかきくけこさしすせそたちつてとなにぬねのはひふへほまみむめもやゆよらりるれろわがぎぐげござじずぜぞだぢづでどばびぶべぼぱぴぷぺぽ))


; (ERB 2002-01-28) Extra application of utterance_rule10 because
; E.MOOD is underspecified out output of this rule.  Changing it
; to indicative.

; (ERB 2002-01-29) Output of this rule should also be SMOD unmarked
; to block ambiguity with nonprop conjunctions.  
; (MS 2004-02-09) BAR -, to block spurious ambiguity in 
; 私が笑う

ru-lexeme-infl-rule := 
word-rule & 
[RMORPH-BIND-TYPE u-morph,
 SYNSEM.LOCAL [ CAT.HEAD verb_head & #head & 
			 [H-TENSE present,
			  MODUS indicative,
			  MOD < [LOCAL [CAT.HEAD noun_head,
					BAR +]] >,
			  C-MOD null,
			  MARK null,
			  J-SPEC < [LOCAL.CAT.HEAD noun_head] >,
			  FIN +,
			  SMOD unmarked,
			  J-POSTHEAD rels,
			  FORMAL.AHON -],
                CONT.HOOK.INDEX.E.MOOD indicative,
		BAR -],
 ARGS.FIRST v-lex & [STEMTYPE regular-stem,
                     SYNSEM.LOCAL.CAT.HEAD #head]].

v-end-ru-infl-rule :=
word-rule & 
[RMORPH-BIND-TYPE u-morph,
 LMORPH-BIND-TYPE #lmorph,
 SYNSEM.LOCAL [CAT.HEAD #head & [H-TENSE present,
                                 C-MOD null,
                                 MOD < [LOCAL [CAT.HEAD noun_head,
                                             BAR +]] >,
                                 J-SPEC < [LOCAL.CAT.HEAD noun_head] >,
                                 FIN +,
                                 J-POSTHEAD rels,
                                 FORMAL.AHON -],
               BAR -],
 ARGS.FIRST lexical_sign & [STEMTYPE regular-stem,
                            LMORPH-BIND-TYPE #lmorph,
                           SYNSEM.LOCAL.CAT.HEAD #head & verbend_head]].

i-lexeme-c-stem-infl-rule := 
%suffix (く き) (る り) (う い) (す し) (む み) (ぐ ぎ) (ぶ び) (つ ち) (ぬ に)
lexeme-rule &
[RMORPH-BIND-TYPE i-morph,
 SYNSEM.LOCAL.CAT.HEAD.MODUS indicative,
 ARGS.FIRST.STEMTYPE c-stem].

;; (FCB 2003-08-07) added u-type 
;; 五段・ワ行ウ音便  --- 乞う 問う
;; (tou and kou - past tense is o-u-ta not o-t-ta)
;; 五段・ラ行特殊 連用形 --- いらっしゃる
;; 五段・カ行促音便 --- 行く
;; only ラ行特殊 連用形 is irregular here:  いらっしゃる → いらっしゃいます

i-lexeme-c2-stem-infl-rule := 
%suffix (く き) (る い) (う い)
lexeme-rule &
[RMORPH-BIND-TYPE i-morph,
 SYNSEM.LOCAL.CAT.HEAD.MODUS indicative,
 ARGS.FIRST.STEMTYPE c2-stem].

;; (SF 2003-08-14) added more forms
;; 実際にlexicon に登録されている物かどうかはチェックせず

;; (MS 2003-12-11) These can be restricted a bit.

i-lexeme-v-stem-infl-rule := 
%suffix (!vる !v) (ケる ケ) (レる レ) (以る 以) (惟る 惟) (易る 易) (違る 違) (越る 越) (厭る 厭) (下る 下) (化る 化) (架る 架) (獲る 獲) (掛る 掛) (干る 干) (換る 換) (看る 看) (観る 観) (気る 気) (戯る 戯) (詰る 詰) (居る 居) (駈る 駈) (経る 経) (迎る 迎) (兼る 兼) (懸る 懸) (見る 見) (枯る 枯) (顧る 顧) (控る 控) (構る 構) (絞る 絞) (似る 似) (射る 射) (捨る 捨) (煮る 煮) (受る 受) (集る 集) (出る 出) (潤る 潤) (初る 初) (消る 消) (焦る 焦) (省る 省) (上る 上) (診る 診) (整る 整) (生る 生) (染る 染) (相る 相) (替る 替) (着る 着) (鋳る 鋳) (著る 著) (伝る 伝) (投る 投) (当る 当) (得る 得) (入る 入) (抜る 抜) (卑る 卑) (簸る 簸) (付る 付) (分る 分) (閉る 閉) (変る 変) (暮る 暮) (崩る 崩) (忘る 忘) (揚る 揚) (溶る 溶) (来る 来) (落る 落) (覧る 覧) (立る 立) (留る 留) (變る 變) (熾る 熾) (睨る 睨) (缺る 缺) (虧る 虧) (覽る 覽) (觀る 觀) (躾る 躾) (酖る 酖) (鑄る 鑄) (驅る 驅) (寝る 寝) (寢る 寢)
lexeme-rule &
[RMORPH-BIND-TYPE vstem-morph,
 ARGS.FIRST.STEMTYPE v-stem].

a-lexeme-negative-cons-stem-infl-rule := 
%suffix (く か) (る ら) (う わ) (す さ) (む ま) (ぐ が) (ぶ ば) (つ た)  (死ぬ 死)
lexeme-rule &
[RMORPH-BIND-TYPE a-or-aa-morph,
ARGS.FIRST.STEMTYPE cons-stem].

pass-lexeme-stem-infl-rule := 
%suffix (く か) (る ら) (う わ) (す さ) (む ま) (ぐ が) (ぶ ば) (つ た) (ぬ な)
lexeme-rule &
[RMORPH-BIND-TYPE pass-c-stem-morph,
ARGS.FIRST.STEMTYPE cons-stem].


;; 　引く→引い て; 笑う→笑っ て; 
t-lexeme-c-stem-infl-rule := 
%suffix (く い) (る っ) (う っ) (す し) (つ っ)
lexeme-rule &
[RMORPH-BIND-TYPE t-morph,
 SYNSEM.LOCAL.CAT.HEAD #1,
 ARGS.FIRST [STEMTYPE c-stem,
	     SYNSEM.LOCAL.CAT.HEAD #1]].

;; (FCB 2003-08-07) added u-type (tou and kou - past tense is o-u-ta not o-t-ta)
; (MS 2005-09-22) it is better to list all c2-stem ending in う, otherwise we
; get a runaway rule!

tt-lexeme-c2-stem-infl-rule := 
%suffix (く っ) (る っ) (問う 問う) (乞う 乞う)
lexeme-rule &
[RMORPH-BIND-TYPE t-morph,
 ARGS.FIRST.STEMTYPE c2-stem].


; (MS 2004-12-21) We need this! 飲めます

pot-lexeme-cons-infl-rule := 
%suffix (く け) (る れ) (う え) (す せ) (む め) (ぐ げ) (ぶ べ) (つ て) (ぬ ね)
potential-rule &
[RMORPH-BIND-TYPE i-or-t-or-a-morph,
ARGS.FIRST.STEMTYPE cons-stem].


;  (MS 2004-12-21) Maybe we don't need this one any more?
;pot-lexeme-v-infl-rule := 
;%suffix (!vる !vられ)
;potential-rule &
;[RMORPH-BIND-TYPE e-morph,
;ARGS.FIRST.STEMTYPE v-stem].

cond-lexeme-infl-rule := 
%suffix (く け) (る れ) (う え) (す せ) (む め) (ぐ げ) (ぶ べ) (つ て) (ぬ ね) (れる れ) (せる せ)
lexeme-rule &
[RMORPH-BIND-TYPE cond-morph,
ARGS.FIRST.STEMTYPE cons-stem].

;(MS 2002-12-02) In spoken language, one can find 食べれば
; (MS 2004-11-24) Extended these to all v-stem verbs

cond-spoken-lexeme-infl-rule := 
%suffix (!vる !v)
lexeme-rule &
[RMORPH-BIND-TYPE cond-exceptional-morph,
 ARGS.FIRST.STEMTYPE v-stem,
 SYNSEM.NON-LOCAL.ROBUST +].


o-lexeme-c-stem-infl-rule := 
%suffix (く こ) (る ろ) (う お) (す そ) (む も) (ぐ ご) (ぶ ぼ) (つ と) (ぬ の)
lexeme-rule &
[RMORPH-BIND-TYPE o-morph,
ARGS.FIRST.STEMTYPE cons-stem].

o-lexeme-v-stem-infl-rule := 
%suffix (る よ)
lexeme-rule &
[RMORPH-BIND-TYPE o-morph,
ARGS.FIRST.STEMTYPE v-stem].

; (MS 2004-11-24) It seems that this is not used any more.

;ere-lexeme-c-stem-infl-rule := 
;%suffix (く けれ) (る えれ) (う えれ) (す せれ) (む めれ) (ぐ げれ) (ぶ べれ) (つ てれ)  (ぬ ねれ)
;lexeme-rule &
;[RMORPH-BIND-TYPE ere-morph,
; ARGS.FIRST.STEMTYPE cons-stem].

; (MS 2004-11-24) It seems that this is not used any more.

;ere-lexeme-v-stem-infl-rule := 
;%suffix (る れれ)
;lexeme-rule &
;[RMORPH-BIND-TYPE ere-morph,
; ARGS.FIRST.STEMTYPE v-stem].


nd-lexeme-infl-rule := 
%suffix (む ん) (ぐ い) (ぶ ん) (ぬ ん)
lexeme-rule &
[RMORPH-BIND-TYPE nd-morph,
 ARGS.FIRST.STEMTYPE c-stem].


eru-lexeme-infl-rule := 
%suffix (く ける) (る れる) (う える) (す せる) (む める) (ぐ げる) (ぶ べる) (!v !vられる)  (つ てる) (ぬ ねる)
potential-lexeme-to-word-rule &
[RMORPH-BIND-TYPE u-morph,
 SYNSEM.LOCAL [CAT.HEAD [H-TENSE present,
                         MODUS potential,
                         MOD < [LOCAL [CAT.HEAD noun_head,
                                     BAR +],
				MODIFIED.PERIPH -] >,
                         MARK null,
                         J-SPEC < [LOCAL.CAT.HEAD noun_head]>,
                         FIN +,
                        J-POSTHEAD rels,
                         FORMAL.AHON -],
	       BAR -],
 ARGS.FIRST.STEMTYPE regular-stem].

; (ERB 2001-09-06) Adding COP-ARG - to block 言いです.
; (MS 2001-12-14) Took the MAIN-PRD - restriction out to allow the comma to modify this.
; (ERB 2002-01-14) Try requiring quasi-te forms to be followed
; a comma in order reduce ambiguity introduced by this rule.  General
; strategy: these guys are CMOD none, but get picked up by a special
; comma that has the right CMOD value for the sentence-te-coordination-rule.
;(CH 2005-04-09)
; added the identity of HEAD values (#head).
;(CH 2005-04-09) added [SYNSEM.LOCAL.BAR -] to reduce the ambiguity involving 
; unary-vstem-vend-rule.

;(CH 2005-04-09)
; removed [J-SPEC.FIRST.LOCAL.CAT.HEAD nom_head,] to use this for 
; syntactic V-Vs, too.  Tried making it no_tense.
infinitive-lexeme-v-infl-rule :=
%suffix (!vる !v) (ケる ケ) (レる レ) (以る 以) (惟る 惟) (易る 易) (違る 違) (越る 越) (厭る 厭) (下る 下) (化る 化) (架る 架) (獲る 獲) (掛る 掛) (干る 干) (換る 換) (看る 看) (観る 観) (気る 気) (戯る 戯) (詰る 詰) (居る 居) (駈る 駈) (経る 経) (迎る 迎) (兼る 兼) (懸る 懸) (見る 見) (枯る 枯) (顧る 顧) (控る 控) (構る 構) (絞る 絞) (似る 似) (射る 射) (捨る 捨) (煮る 煮) (受る 受) (集る 集) (出る 出) (潤る 潤) (初る 初) (消る 消) (焦る 焦) (省る 省) (上る 上) (診る 診) (整る 整) (生る 生) (染る 染) (相る 相) (替る 替) (着る 着) (鋳る 鋳) (著る 著) (伝る 伝) (投る 投) (当る 当) (得る 得) (入る 入) (抜る 抜) (卑る 卑) (簸る 簸) (付る 付) (分る 分) (閉る 閉) (変る 変) (暮る 暮) (崩る 崩) (忘る 忘) (揚る 揚) (溶る 溶) (来る 来) (落る 落) (覧る 覧) (立る 立) (留る 留) (變る 變) (熾る 熾) (睨る 睨) (缺る 缺) (虧る 虧) (覽る 覽) (觀る 觀) (躾る 躾) (酖る 酖) (鑄る 鑄) (驅る 驅) (寝る 寝) (寢る 寢)
j-lexeme-to-word-rule &
[RMORPH-BIND-TYPE inf-morph,
 SYNSEM.LOCAL [CAT.HEAD #head &
			[H-TENSE no_tense,
                        MODUS infinitive,
                        MOD null,
                        VN -,
                        C-MOD null,
                        MARK null,
                        J-POSTHEAD coord,
                        COP-ARG -,
                        FIN -],
	BAR -],
 ARGS.FIRST [STEMTYPE v-stem,
             SYNSEM.LOCAL.CAT.HEAD #head & verb-stem_head,
	     RMORPH-BIND-TYPE morphbindtype]].

;(CH 2005-04-09)
; removed [J-SPEC.FIRST.LOCAL.CAT.HEAD nom_head,] to use this for 
; syntactic V-Vs, too.  Tried making it no_tense.
infinitive-lexeme-c-infl-rule :=
%suffix  (く き) (る り) (う い) (す し) (む み) (ぐ ぎ) (ぶ び)  (つ ち) (ぬ に) (見る 見)
j-lexeme-to-word-rule &
[RMORPH-BIND-TYPE inf-morph,
 SYNSEM.LOCAL [CAT.HEAD #head &
			[H-TENSE no_tense,
                        MODUS infinitive,
                        MOD null,
                        VN -,
                        C-MOD null,
                        MARK null,
                        J-POSTHEAD coord,
                        COP-ARG -,
                        FIN -],
	BAR -],
 ARGS.FIRST [STEMTYPE c-stem,
             SYNSEM.LOCAL.CAT.HEAD #head & verb-stem_head,
	     RMORPH-BIND-TYPE morphbindtype]].



infinitive-lexeme-c2-stem-infl-rule := 
%suffix (く き) (る い) (う い)
j-lexeme-to-word-rule &
[RMORPH-BIND-TYPE inf-morph,
 SYNSEM.LOCAL [CAT.HEAD #head &
			[H-TENSE no_tense,
                        MODUS infinitive,
                        MOD null,
                        VN -,
                        C-MOD null,
                        MARK null,
                        J-POSTHEAD coord,
                        COP-ARG -,
                        FIN -],
	BAR -],
 ARGS.FIRST [STEMTYPE c2-stem,
             SYNSEM.LOCAL.CAT.HEAD #head & verb-stem_head,
	     RMORPH-BIND-TYPE morphbindtype]].

; (MS 2003-07-11) We have to find another solution for that.
 
;mai-lexeme-cons-stem-infl-rule := 
;j-lexeme-to-lexeme-rule &
;[RMORPH-BIND-TYPE mai-morph,
;ARGS.FIRST v-lex & [STEMTYPE cons-stem]].


;kudasai
;commented out to prevent too much ambiguity for kudasai. Let's see if this is harmless.
;(MS 2001-10-31) No, this is not possible: kogitte wo kudasai.

;(ERB 2001-11-12) Want to allow main verb kudasaru to be imperative while
;disallowing aux kudasaru to be imperative (since kudasai is now an sap).
;Maybe the feature AUX can be leveraged?

;(TK 07-05-23)imperative-infl-ruleがc-stemとc2-stemにしかない。v-stemやkurusuru-stem、
;丁寧の「ます」にも必要。

imperative-c2-stem-infl-rule :=
%suffix (く け) (る い) (う え)
j-lexeme-to-word-rule &
[RMORPH-BIND-TYPE imp-morph,
 SYNSEM.LOCAL [CAT.HEAD [H-TENSE present,
                         MODUS simple-imperative,
                         VN -,
			 C-MOD null,
                         MOD null,
                         MARK null,
                         J-SPEC null,
			 FIN +],
	       BAR -],
 ARGS.FIRST.STEMTYPE c2-stem].


; (MS 2004-12-15) For example, 止れ, stop

imperative-c-stem-infl-rule :=
%suffix (く け) (る れ) (う え) (す せ) (む め) (ぐ げ) (ぶ べ) (つ て) (ぬ ね)
j-lexeme-to-word-rule &
[RMORPH-BIND-TYPE imp-morph,
 SYNSEM.LOCAL [CAT.HEAD [H-TENSE present,
                         MODUS simple-imperative,
                         VN -,
			 C-MOD null,
                         MOD null,
                         MARK null,
                         J-SPEC null,
                         FIN +],
	       BAR -],
 ARGS.FIRST.STEMTYPE c-stem].


imp-yo-v-stem-infl-rule :=
%suffix (る よ)
j-lexeme-to-word-rule &
[RMORPH-BIND-TYPE imp-morph,
 SYNSEM.LOCAL [CAT.HEAD [H-TENSE present,
                         MODUS simple-imperative,
                         VN -,
			 C-MOD null,
                         MOD null,
                         MARK null,
                         J-SPEC null,
                         FIN +],
	       BAR -],
 ARGS.FIRST.STEMTYPE v-stem].

imp-ro-v-stem-infl-rule :=
%suffix (る ろ)
j-lexeme-to-word-rule &
[RMORPH-BIND-TYPE imp-morph,
 SYNSEM.LOCAL [CAT.HEAD [H-TENSE present,
                         MODUS simple-imperative,
                         VN -,
			 C-MOD null,
                         MOD null,
                         MARK null,
                         J-SPEC null,
                         FIN +],
	       BAR -],
 ARGS.FIRST.STEMTYPE v-stem].


;;;;;;;;;;;;;;;;;;;COPULA;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; (FCB 2004-02-08) BAR -, to block spurious ambiguity in 
; きれい な 犬 
; (CHECKME: + also works (no difference in cover, with one less rule)

desu-lexeme-infl-rule :=
word-rule & 
[RMORPH-BIND-TYPE u-morph,
 SYNSEM.LOCAL [CAT.HEAD verb_head & [H-TENSE present,
                                     MODUS indicative,
                                     C-MOD null,
                                     MOD #mod,
                                     MARK null,
                                     J-SPEC.FIRST.LOCAL.CAT.HEAD nom_head,
                                     J-POSTHEAD rels,
                                     FIN +],
	       BAR -],
 ARGS.FIRST v-lex & [STEMTYPE cop-stem,
                     SYNSEM.LOCAL [CAT.HEAD.MOD #mod]]].

; (ERB 2001-11-12) Experimenting with making te form TENSE tensed
; instead of TENSE te.

; (ERB 2002-02-12) To reduce ambiguity, treat で form of です like
; output of infinitive-lexeme-1-infl-rule.  That is, it must have
; comma to be able to be conjoined with another clause.  Trick is
; to make it C-MOD null.  To make this work, also needs to be
; MODUS infinitive.

;de-lexeme-infl-rule :=
;%suffix (です で)
; j-lexeme-to-word-rule &
;[RMORPH-BIND-TYPE u-morph,
; SYNSEM.LOCAL.CAT.HEAD [H-TENSE tensed,
;                         MODUS indicative,
;                         MOD null,
;			 VN -,
;			 J-POSTHEAD coord,
;                         C-MOD < [LOCAL.CAT.HEAD verb_head & [MAIN-PRD +]] >,
;                         MARK null,
;                         J-SPEC.FIRST.LOCAL [CAT.HEAD verb_head & [AUX +],
;                                     LEX +],
;                         FIN -],
; ARGS.FIRST.STEMTYPE desu-stem].

de-lexeme-infl-rule :=
%suffix (です で)
 j-lexeme-to-word-rule &
[RMORPH-BIND-TYPE u-morph,
 SYNSEM [LOCAL.CAT.HEAD [H-TENSE tensed,
                         MODUS infinitive,
                         MOD null,
			 VN -,
			 J-POSTHEAD coord,
                         C-MOD null,
                         MARK null,
                         J-SPEC.FIRST.LOCAL [CAT.HEAD verb_head & [AUX aux_plus]],
                         FIN -],
	 LEX +],
 ARGS.FIRST.STEMTYPE desu-stem].

ra-lexeme-infl-rule :=
%suffix (な なら) (なが ながら)
conditional-lexeme-to-word-rule &
[RMORPH-BIND-TYPE u-morph,
 SYNSEM.LOCAL [CAT [HEAD [H-TENSE present,
			 MODUS conditional,
			 MOD null,
			 VN -,
			 MARK null,
			 J-SPEC.FIRST [LOCAL [CAT.HEAD verb_head & [AUX aux_plus]],
				     LEX +],
			 FIN -]],
	       BAR -],
 ARGS.FIRST.STEMTYPE da-stem].




;da-lexeme-infl-rule := 
;word-rule & 
;[RMORPH-BIND-TYPE u-morph,
; SYNSEM.LOCAL.CAT.HEAD verb_head & [H-TENSE present,
;                                    MODUS indicative,
;                                    MOD < [LOCAL [CAT.HEAD noun_head,
;                                                BAR +]] >,
;                                    MARK null,
;                                    J-SPEC.FIRST.LOCAL.CAT.HEAD noun_head,
;                                    FIN +,
;                                    J-POSTHEAD rels,
;                                    FORMAL.AHON -],
; ARGS.FIRST v-lex & [STEMTYPE da-stem]].

tt-cop-lexeme-infl-rule := 
%suffix (だ だっ) (す し)
lexeme-rule &
[RMORPH-BIND-TYPE t-morph,
 ARGS.FIRST.STEMTYPE cop-stem].

o-cop-lexeme-infl-rule := 
%suffix (す しょ) (だ だろ)
lexeme-rule &
[RMORPH-BIND-TYPE o-morph,
ARGS.FIRST.STEMTYPE cop-stem].




;;;;;;;;;;;;;;;;;;;KURU-SURU;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

; (MS 2004-10-05) BAR -, to block spurious ambiguity in 来る 

kuru-lexeme-infl-rule := 
word-rule & 
[RMORPH-BIND-TYPE u-morph,
 SYNSEM.LOCAL [CAT.HEAD verb_head & [H-TENSE present,
				  MODUS indicative,
				  MOD < [LOCAL [CAT.HEAD noun_head,
						BAR +]] >,
				     MARK null,
				     J-SPEC.FIRST.LOCAL.CAT.HEAD noun_head,
				     FIN +,
				     J-POSTHEAD rels,
				     FORMAL.AHON -],
	       BAR -],
 ARGS.FIRST v-lex & [STEMTYPE kurusuru-stem]].

ki-lexeme-infl-rule := 
%suffix (来る 来) (する し) (くる き)
lexeme-rule &
[RMORPH-BIND-TYPE i-morph,
 SYNSEM.LOCAL.CAT.HEAD.MODUS indicative,
 ARGS.FIRST.STEMTYPE kurusuru-stem].

ka-lexeme-infl-rule := 
%suffix (来る 来) (する し) (くる こ)
lexeme-rule &
[RMORPH-BIND-TYPE a-morph,
ARGS.FIRST.STEMTYPE kurusuru-stem].

;;; (FCB 2003-08-12) rule for "se zu" very irregular suru ending before zu

kaa-lexeme-infl-rule := 
%suffix (来る 来) (する せ) (くる こ)
lexeme-rule &
[RMORPH-BIND-TYPE aa-morph,
ARGS.FIRST.STEMTYPE kurusuru-stem].

kit-lexeme-c-stem-infl-rule := 
%suffix (来る 来) (する し) (くる き)
lexeme-rule &
[RMORPH-BIND-TYPE t-morph,
 ARGS.FIRST.STEMTYPE kurusuru-stem].

ke-lexeme-infl-rule := 
%suffix (る れ)
lexeme-rule &
[RMORPH-BIND-TYPE cond-morph,
ARGS.FIRST.STEMTYPE kurusuru-stem].

; (TK 07-05-25)"(する しよ)"の形に合わせるなら"(来る 来よ)""(くる こよ)"
; にすべき。（現状だと「来 う」「こ う」が解析できてしまう。）
ko-lexeme-infl-rule := 
%suffix (来る 来) (くる こ) (する しよ)
lexeme-rule &
[RMORPH-BIND-TYPE o-morph,
ARGS.FIRST.STEMTYPE kurusuru-stem].

sa-lexeme-infl-rule := 
%suffix (する さ) (来る 来さ) (くる こさ)
lexeme-rule &
[RMORPH-BIND-TYPE pass-c-stem-morph,
ARGS.FIRST.STEMTYPE kurusuru-stem].

; (ERB 2001-09-06) Adding COP-ARG - to block 来です.

; (ERB 2001-11-12) Experimenting with making te form TENSE tensed
; instead of TENSE te.

infinitive-lexeme-2-infl-rule :=
%suffix (来る 来) (する し) (くる こ)
j-lexeme-to-word-rule &
[RMORPH-BIND-TYPE inf-morph,
 SYNSEM.LOCAL [CAT.HEAD verb_head-avm & [H-TENSE tensed,
                                        MODUS infinitive,
                                        MOD < [LOCAL [CAT [HEAD verb_head & [MAIN-PRD +]],
                                                   BAR +] ] >,
                                        J-POSTHEAD rels,
                                        MARK null,
                                        VN -,
                                        J-SPEC null,
                                        COP-ARG -,
                                        FIN -]],
 ARGS.FIRST.STEMTYPE kurusuru-stem].

;;; FIXME should this be untensed?
mai-beki-lexeme-regular-infl-rule := 
lexeme-rule &
 [RMORPH-BIND-TYPE mai-beki-morph,
  SYNSEM.LOCAL.CAT.HEAD [MOD < [LOCAL [CAT.HEAD noun_head,
 				       BAR +]] >,
			 MARK null,
			 MODUS indicative,
			 FIN -,
			 SMOD unmarked,
			 J-POSTHEAD rels,
			 FORMAL.AHON -],
  ARGS.FIRST v-lex & [STEMTYPE regular-stem]].


; (TK 07-05-24)kurusuru-stem以外のverbに対応するものも必要。
; また、同じkurusuru-stemでも、「論ずる」など語尾が「ずる」のものに対応できていない。

mai-lexeme-ks-infl-rule := 
%suffix (来る 来) (する す) (くる こ)
lexeme-rule &
[RMORPH-BIND-TYPE mai-morph,
 SYNSEM.LOCAL.CAT.HEAD.MODUS indicative,
ARGS.FIRST v-lex & [STEMTYPE kurusuru-stem]].

; (MS 2004-12-17) beki takes す in the case of する+べき: すべき

beki-lexeme-ks-infl-rule := 
%suffix (する す) (来る 来る) (くる くる)
lexeme-rule &
[RMORPH-BIND-TYPE beki-morph,
 SYNSEM.LOCAL.CAT.HEAD.MODUS indicative,
 ARGS.FIRST.STEMTYPE kurusuru-stem].




;;;;;;;;;;;;;;;;;;;;;;;;ADJECTIVES;;;;;;;;;;;;;;;;;;;;

; (ERB 2002-01-22) Try making these work
; so that what shows up in the language server is the
; dictionary form of adjectives.  
;
; Have te and past rules replace ending with い,
; adj-i-lexeme has no phonological effect.


adj-te-t-lexeme-c-stem-infl-rule := 
%suffix (い く)
lexeme-rule &
[RMORPH-BIND-TYPE te-morph,
 ARGS.FIRST.STEMTYPE adj-stem,
 SYNSEM.LOCAL.CAT.HEAD.H-TENSE te].
; SYNSEM.LOCAL.CAT.HEAD.MARK < [LOCAL.CAT.HEAD.H-TENSE te] >].


adj-past-t-lexeme-c-stem-infl-rule := 
%suffix (い かっ)
lexeme-rule &
[RMORPH-BIND-TYPE ta-morph,
 ARGS.FIRST.STEMTYPE adj-stem,
 SYNSEM.LOCAL.CAT.HEAD.H-TENSE past].
; SYNSEM.LOCAL.CAT.HEAD.MARK < [LOCAL.CAT.HEAD.H-TENSE past] >].



; (ERB 2002-01-02) We no longer want to say that 
; adjectives (i.e., relative clauses) modify only 
; N'.  Removing SPR_SAT unsat from MOD value.

; (ERB 2002-01-14) Output of this rule needs to be SMOD
; unmarked to restrict application of non-prop GA.
; (ERB 2002-01-28) Need to copy up HEAD value like in 
; ru-lexeme-infl-rule, but the J-SPEC value given here
; (J-SPEC.FIRST.LOCAL.CAT.HEAD noun_head) is incompatible with what's
; in adj-stem-lex.  Try taking that out of rule for now.

; (MS 2002-01-31) Changed that rule a little bit to account for: 
; 良いことだ     元気ではない
; (MS 2002-02-01) But this leads to ambiguity in cases of ADJ NO DESU KA. 
; What shall we do?
; (MS 2004-11-09) J-NEEDS-AFFIX + on the argument, such that it doesn't apply
; to 食べていない . I further restrict it to adj-stems.
; (MS 2005-06-20) I don't think that this should be AHON -. How about "yoroshii"?

adj-i-lexeme-infl-rule := 
word-rule &
[RMORPH-BIND-TYPE u-morph,
 ARGS.FIRST [STEMTYPE adj-stem,
	     SYNSEM.LOCAL.CAT.HEAD i-adj-stem_head & [MODUS #modus,
						      FORMAL #form],
	     J-NEEDS-AFFIX +],
 SYNSEM.LOCAL [CAT.HEAD i-adj-stem_head & 
			[H-TENSE present,
                         MODUS #modus,
                         MOD < [LOCAL [CAT [HEAD noun_head],
                                     BAR +]] >,
                         J-POSTHEAD rels,
                         MARK null,
			 J-SPEC <[LOCAL [CAT [HEAD noun_head],
                                     BAR +]]>,
                         SMOD unmarked,
                         VN -,
                         C-MOD null,
                         FIN +,
                         FORMAL #form],
	       BAR -]].

; (MS 2005-10-20) ない can be ね as well as なけれ:
; 軍事上の機密は守られねばならない。

adj-kere-lexeme-infl-rule := 
%suffix (い けれ) (ない ね)
lexeme-rule &
[RMORPH-BIND-TYPE cond-morph,
 ARGS.FIRST.STEMTYPE adj-stem].
; SYNSEM.LOCAL.CAT.HEAD.MODUS conditional].
; SYNSEM.LOCAL.CAT.HEAD.MARK < [LOCAL.CAT.HEAD.MODUS conditional] >].


a-lexeme-adj-stem-infl-rule := 
%suffix (い く)
lexeme-rule &
[RMORPH-BIND-TYPE a-morph,
 SYNSEM.LOCAL.CAT.HEAD i-adj-stem_head, 
 ARGS.FIRST[ STEMTYPE adj-stem,
	     SYNSEM.LOCAL.CAT.HEAD i-adj-stem_head]].

adj2adv-lexeme-infl-rule := 
%suffix (い く)
adj2adv-rule &
[ARGS.FIRST.STEMTYPE adj-stem].

; (ERB 2001-10-18) Commenting out this rule so I can load the grammar.
; (ERB 2002-01-28) Using the dictionary form of adjectives as stems
; seems to have broken this rule.  Commenting out for now...
; Also commenting out its ranking in japanese.set.  Uncomment that
; if this gets put back in!!
; (MS 2002-02-01) This is needed for, e.g., "宜しそうです", but had to be modified.
; The problem is here (again) that there is no straightforward way to state 
; 'eliminate the end い for all kanji characters' in these rules. This is what I wanted to state:

;; (!hい !h) (少い 少さ) (ない なさ) (速い 速) ...

; But it lead to never ending parses. These guys don't seem to work:
;;  (くい く)
;;  (つい い) 
;;  (るい る)
;;  (うい う)

;; (SF 2003-08-14) added more forms
;; 実際にlexicon に登録されている物かどうかはチェックせず

;; (MS 2003-12-10) entries like (うれしい うれし)  are not necessary.
;; Those are cared for by (しい し).

adj-short-infl-rule :=
%suffix (しい し) (さい さ) (たい た) (まい ま) (ずい ず) (ない な) (どい ど) (とい と) (こい こ) (ちい ち) (らい ら) (よい よ) (ゆい ゆ) (つい つ)(ごい ご) (ろい ろ) (すい す) (そい そ) (かい か) (ぱい ぱ) (ぽい ぽ) (わい わ) (じい じ) (きい き) (げい げ) (愛い 愛) (悪い 悪) (渥い 渥) (安い 安) (暗い 暗) (偉い 偉) (易い 易) (映い 映) (永い 永) (鋭い 鋭) (円い 円) (煙い 煙) (遠い 遠) (汚い 汚) (温い 温) (佳い 佳) (快い 快) (恢い 恢) (晦い 晦) (角い 角) (赫い 赫) (寒い 寒) (緩い 緩) (丸い 丸) (危い 危さ) (貴い 貴) (旧い 旧) (喬い 喬) (強い 強) (恐い 恐) (狭い 狭) (近い 近) (苦い 苦) (屑い 屑) (軽い 軽) (潔い 潔) (堅い 堅) (賢い 賢) (古い 古) (固い 固) (厚い 厚) (好い 好) (宏い 宏) (巧い 巧) (広い 広) (弘い 弘) (硬い 硬) (紅い 紅) (荒い 荒) (高い 高) (豪い 豪) (酷い 酷) (黒い 黒) (細い 細) (惨い 惨) (酸い 酸) (旨い 旨) (疾い 疾) (若い 若) (弱い 弱) (手い 手) (臭い 臭)  (醜い 醜) (渋い 渋) (重い 重) (暑い 暑) (少い 少) (少い 少さ) (浄い 浄) (色い 色) (深い 深)  (辛い 辛) (凄い 凄) (星い 星) (清い 清) (青い 青) (脆い 脆) (赤い 赤) (拙い 拙) (浅い 浅) (善い 善) (疎い 疎) (粗い 粗) (早い 早) (聡い 聡) (草い 草) (蒼い 蒼) (憎い 憎) (速い 速) (尊い 尊) (多い 多) (太い 太) (堆い 堆) (怠い 怠) (淡い 淡) (短い 短) (暖い 暖) (稚い 稚) (遅い 遅) (長い 長) (痛い 痛) (度い 度) (篤い 篤) (鈍い 鈍) (難い 難) (濃い 濃) (蝿い 蝿) (博い 博) (白い 白) (薄い 薄) (繁い 繁) (煩い 煩)  (晩い 晩) (敏い 敏) (怖い 怖) (殆い 殆) (味い 味) (眠い 眠) (無い 無) (憂い 憂) (幼い 幼) (良い 良) (良し 良) (廉い 廉) (俐い 俐) (儚い 儚) (圓い 圓) (夛い 夛) (梦い 梦) (嫖い 嫖) (嬾い 嬾) (孅い 孅) (尠い 尠) (孱い 孱) (岌い 岌) (嶷い 嶷) (廣い 廣) (惡い 惡) (慝い 慝) (慵い 慵) (懶い 懶) (擽い 擽) (敞い 敞) (暄い 暄) (暝い 暝) (曚い 曚) (毋い 毋) (泓い 泓) (洌い 洌) (淨い 淨) (淺い 淺) (溽い 溽) (澁い 澁) (澀い 澀) (熈い 熈) (狡い 狡) (狹い 狹) (痒い 痒) (癢い 癢) (皎い 皎) (眩い 眩) (矗い 矗) (穉い 穉) (穢い 穢) (糲い 糲) (纎い 纎) (譱い 譱) (聰い 聰) (腆い 腆) (腥い 腥) (臧い 臧) (舊い 舊) (褊い 褊) (輕い 輕) (邇い 邇) (逖い 逖) (遐い 遐) (遲い 遲) (陜い 陜) (駛い 駛) (鹹い 鹹) (黔い 黔) (黠い 黠) (危うい 危う) (殆うい 殆う)  (にくい にく)(ぬくい ぬく)(あつい あつ)(かつい かつ) (めつい めつ)  (きつい きつ) (ぎつい ぎつ)(たるい たる)  (だるい だる) (かるい かる)  (ずるい ずる) (ぬるい ぬる)  (まるい まる)  (わるい わる) (やうい やう)
j-lexeme-to-word-rule &
[RMORPH-BIND-TYPE shortadj-morph,
 ARGS.FIRST.STEMTYPE adj-stem,
 SYNSEM.LOCAL.CAT.HEAD i-adj-stem_head &
		  [MOD null,
		   MARK null,
		   FIN -]].
;lexeme-rule &


adj-ku-lexeme-infl-rule := 
%suffix (い く)
j-lexeme-to-word-rule &
[RMORPH-BIND-TYPE u-morph,
 ARGS.FIRST [STEMTYPE adj-stem,
	     SYNSEM.LOCAL.CAT.HEAD i-adj-stem_head & [MODUS #modus],
	     J-NEEDS-AFFIX +],
 SYNSEM.LOCAL [CAT.HEAD i-adj-stem_head & 
			[H-TENSE te,
                         MODUS #modus,
                         MOD null,
                         J-POSTHEAD rels,
                         MARK null,
			 J-SPEC null,
                         SMOD unmarked,
                         VN -,
                         C-MOD < [LOCAL.CAT.HEAD sentence-valid] >,
                         FIN -,
                         FORMAL.AHON -,
			 COP-ARG -],
	       BAR -]].



;;;;;;;;;;;;;;;;;;;;;;;;ADJ->V;;;;;;;;;;;;;;;;;;;;

;;; FCB 2008-06-13: I expect this to be segmented off
;adj2v-infl-rule :=
;%suffix (い 過ぎる)
;adj2v-rule &
;[RMORPH-BIND-TYPE u-morph].

;;;;;;;;;;;;;;;;;;;;;;;;ADJ->N;;;;;;;;;;;;;;;;;;;;

; (ERB 2002-01-22) Chasen segments off さ anyway,
; so commenting this out for now.

;adj2n-infl-rule :=
;%suffix (い さ)
;adj2n-rule &
;[ARGS.FIRST.STEMTYPE adj-stem].

;;;;;;;;;;;;;;;;;;;;;;;;V->VN;;;;;;;;;;;;;;;;;;;;

v2vn-v-infl-rule :=
%suffix (!vる !v) 
v2vn-rule &
[ARGS.FIRST [STEMTYPE v-stem,
             RMORPH-BIND-TYPE morphbindtype,
             INFLECTED -]].

v2vn-c-infl-rule :=
%suffix (く き) (る り) (う い) (す し) (む み) (ぐ ぎ) (ぶ び)  (つ ち)
v2vn-rule &
[ARGS.FIRST [STEMTYPE c-stem,
             RMORPH-BIND-TYPE morphbindtype,
             INFLECTED -]].

v2vn-c2-stem-infl-rule := 
%suffix (く き) (る い) (う い)
v2vn-rule &
[ARGS.FIRST [STEMTYPE c2-stem,
             RMORPH-BIND-TYPE morphbindtype,
             INFLECTED -]].

;;;;;;;;;;;;;;;;;;;;;;;;V->N;;;;;;;;;;;;;;;;;;;;

; (MS 2005-09-26) In generation, we allowed 食べリ方
; so we now have two rules here.

v2n-vstem-kata-infl-rule :=
%suffix (!vる !v方)
v2n-kata-rule &
[ARGS.FIRST [STEMTYPE v-stem,
             RMORPH-BIND-TYPE morphbindtype,
             INFLECTED -]].

v2n-cstem-kata-infl-rule :=
%suffix (く き方) (る り方) (う い方) (す し方) (む み方) (ぐ ぎ方) (ぶ び方)  (つ ち方) (ぬ に方)
v2n-kata-rule &
[ARGS.FIRST [STEMTYPE cons-stem,
             RMORPH-BIND-TYPE morphbindtype,
             INFLECTED -]].




;;;;;;;;;;;;;;;;;;;;;;;; VN, N and ADJ POLITE ;;;;;;;;;;;;;;;;

; (ERB 2002-01-26) Restrict this rule to specific lexical entries,
; to reduce the number of 'words' parser has to consider.  Chasen
; seems to split the o- prefix off most of the time.

; (ERB 2002-02-11) Uli made a fix to cheap that blocks impossible
; chains of inflectional rules.  Try taking away lexical restriction
; here to see how bad it gets now.

; (MS 2003-11-18) This solution made problems with the generation. Used the 
; BACKGROUND <! !> restriction because of that.

; (FCB 2003-12-05) Taking away the lexical restriction makes "おく"
; explode.  So I'm restricting these again.

;;;;;;;;;;;;;;;;;;;;;;;;VN POLITE;;;;;;;;;;;;;;;;;;;;
vn-polite-infl-rule :=
%prefix (知らせ お知らせ) (願い お願い) (送り お送り) (返し お返し) (電話 お電話)
vnpolite-rule &
[ARGS.FIRST.SYNSEM.LOCAL.CAT.HEAD vn_head].

; vn-polite-infl-rule :=
; %prefix (* お)
; vnpolite-rule &
; [ARGS.FIRST.SYNSEM.LOCAL [CAT.HEAD vn_head,
; 			  CTXT.BACKGROUND <! !>]].

;vn-polite-infl-rule :=
;%prefix (* お)
;vnpolite-rule &
;[ARGS.FIRST.SYNSEM.LOCAL.CAT [HEAD vn_head,
;			      VAL.UNSAT +]].

;;;;;;;;;;;;;;;;;;;;;;;;N POLITE;;;;;;;;;;;;;;;;;;;;

n-polite-infl-rule :=
%prefix (世話 お世話) (昼 お昼) (金 お金) (手数 お手数)
npolite-rule &
[ARGS.FIRST.SYNSEM.LOCAL.CAT.HEAD ordinary_noun_head].

; n-polite-infl-rule :=
; %prefix (* お)
; npolite-rule &
; [ARGS.FIRST.SYNSEM.LOCAL [CAT.HEAD ordinary_noun_head,
; 			  CTXT.BACKGROUND <! !>]].

;;;;;;;;;;;;;;;;;;;;;;;;ADJ POLITE;;;;;;;;;;;;;;;;;;;;

adj-polite-infl-rule :=
%prefix (忙しい お忙しい)
adjpolite-rule &
[ARGS.FIRST.SYNSEM.LOCAL.CAT.HEAD i-adj-stem_head].

; adj-polite-infl-rule :=
; %prefix (* お)
; adjpolite-rule &
; [ARGS.FIRST.SYNSEM.LOCAL [CAT.HEAD i-adj-stem_head,
; 			  CTXT.BACKGROUND <! !>]].



;;;;;;;;;;;;;;;;;;;;;;;;NA -> ADV;;;;;;;;;;;;;;;;;;;;


;na2adv-lrule :=
;na2adv-rule &
;[ARGS.FIRST.SYNSEM.LOCAL.CAT.HEAD na-adj_head].


;;;;;;;;;;;;;;;;;;;;;;;;N -> ADJ;;;;;;;;;;;;;;;;;;;;

;n2adj-lrule :=
;%suffix (* 的)
;n2adj-rule &
;[ARGS.FIRST.SYNSEM.LOCAL.CAT.HEAD ordinary_noun_head].
; fix me - this doesn't appear to work
n2i-adj-lrule :=
%suffix (* らしい)
n2i-adj-rule &
[ARGS.FIRST.SYNSEM.LOCAL.CAT.HEAD ordinary_noun_head].

;(CH 2003-08-25)
;「動物的人間」など「的」が直接名詞にかかれるようにするために追加。
; 「〜的」を na-adj-lex ではなく pred-adj-lex とする。
; (cf. n2predadj-rule in rule-types.tdl)
; しかし、「動物的人間」などでは、「動物的な人間」と違い、
; 「動物的」と「人間」の topic-rel をうまく作れない。
; ちなみに、もとからできてる「大きな人間」でも topic-rel は
; うまく作れない。
; fixme
n2predadj-lrule :=
%suffix (* 的)
n2predadj-rule &
[ARGS.FIRST.SYNSEM.LOCAL.CAT.HEAD ordinary_noun_head].


;;;;;;;;;;;;;;;;;;;;;V Compounds;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;(CH 2003-08-27)
; 統語的複合動詞の V1 のための infl-rule 
; infinitive-lexeme-1-infl-rule の [J-SPEC.FIRST.LOCAL.CAT.HEAD nom_head]の
; nom_head を final_head にしただけ。
; (cf. syntactic-vv-stem_head, syntactic-vv-stem-lex in lex-types.tdl
;      tsudukeru-syntactic-vv-stem in verbstem-lex.tdl)
;(CH 2005-03-30)
; added the identity of HEAD values (#head).
;(CH 2005-04-09) added [SYNSEM.LOCAL.BAR -] to reduce the ambiguity involving 
; unary-vstem-vend-rule.
;(CH 2005-04-09)
; removed because revised infinitive-lexeme-1-infl-rule should take care of
; syntactic V-Vs, too.
; infinitive-infl-rule-for-syntactic-VV :=
; %suffix (!vる !v) (く き) (る り) (う い) (す し) (む み) (ぐ ぎ) (ぶ び)  (つ ち) (ぬ に)
; j-lexeme-to-word-rule &
; [RMORPH-BIND-TYPE inf-morph,
;  SYNSEM.LOCAL [CAT.HEAD #head &
; 			[H-TENSE tensed,
;                         MODUS infinitive,
;                         MOD null,
;                         VN -,
;                         C-MOD null,
;                         MARK null,
;                         J-SPEC.FIRST.LOCAL.CAT.HEAD syntactic-vv-stem_head,
;                         J-POSTHEAD coord,
;                         COP-ARG -,
;                         FIN -],
; 		BAR -],
;  ARGS.FIRST [STEMTYPE regular-stem,
;              SYNSEM.LOCAL.CAT.HEAD #head & verb-stem_head,
; 	     RMORPH-BIND-TYPE morphbindtype]].


