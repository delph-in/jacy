;;; -*- Mode: TDL; Package: DISCO -*-

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;        file: %Z% %M%
;;;      module:
;;;     version: %I%
;;;  written by:
;;; last update: %G% --- %U%
;;;  updated by: 
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;; author            | date        | modification
;;; ------------------|-------------|------------------------------------------
;;;    Melanie Siegel |    2003     |Francis Bond (FCB), 
;;;                   |             |Emily Bender (ERB), Sanae Fujita (SF), 
;;;                   |             | Takaaki Tanaka (TT)
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;




;finite-present-polite-indicative          

; (ERB 2002-01-02) Allow ます-ending relative clauses;
; make this have the same MOD value as the output of the
; ru-lexeme-infl-rule. Also make it [POSTHEAD rels] to
; keep it out of the other head-adjunct rules.
;(MS 2002-04-25) I found examples with ーますよう. In order to account for this, I inserted a J-SPEC possibility to ます

masu-end  := v-present-tense-end-lex &
             [ORTH <! "ます" !>,
	      SYNSEM.LOCAL.CAT.HEAD [FORMAL.AHON +,
				     J-SPEC < [LOCAL.CAT.HEAD exceptional_noun_head] >],
	      LMORPH-BIND-TYPE i-morph].




;finite-past-polite-indicative

mashita-end :=  v-past-tense-end-lex &
              [ORTH <! "まし", "た" !>,
               SYNSEM.LOCAL [CAT [HEAD [J-SPEC < [LOCAL.CAT.HEAD nom_head] >,
					FORMAL.AHON +,
                                        H-TENSE past]]],
               LMORPH-BIND-TYPE i-morph].

;te-polite-indicative

; (ERB 2001-11-12) Experimenting with making te form TENSE tensed
; instead of TENSE te.

mashite-end  :=  v-te-end-lex &
                 [ORTH <! "まし", "て" !>,
		  SYNSEM.LOCAL.CAT.HEAD.FORMAL.AHON +,
		  LMORPH-BIND-TYPE i-morph].

;another segmentation of CHASEN

; (ERB 2001-11-12) Experimenting with making te form TENSE tensed
; instead of TENSE te.

mashite-end2  :=  v-te-end-lex &
                 [ORTH <! "まして" !>,
		  SYNSEM.LOCAL.CAT.HEAD.FORMAL.AHON +,
                  LMORPH-BIND-TYPE i-morph].


;finite-past-plain-indicative

ta-end := v-past-tense-end-lex &
           [ORTH <! "た" !>,
            SYNSEM.LOCAL [CAT [HEAD [J-SPEC < [LOCAL.CAT.HEAD nom_head] >,
                                     FORMAL.AHON -]]],
            LMORPH-BIND-TYPE t-morph,
            INFLECTED +].

da-end := v-past-tense-end-lex &
           [ORTH <! "だ" !>,
            SYNSEM.LOCAL [CAT [HEAD [J-SPEC < [LOCAL.CAT.HEAD nom_head] >,
				     FORMAL.AHON -]]],
            LMORPH-BIND-TYPE nd-morph,
            INFLECTED +].

; (ERB 2002-01-22) These two should be C-MOD none, if they'_re combining
; via the MOD value.  (Our analysis of Vtari Vtari suru might need
; some work.)

;nonfinite-present-plain-indicative

tari-end := v-coord-end-lex &
            [ORTH <! "たり" !>,
             LMORPH-BIND-TYPE t-morph].

;nonfinite-present-plain-indicative

dari-end := v-coord-end-lex &
           [ORTH <! "だり" !>,
	    LMORPH-BIND-TYPE nd-morph].

;te-plain-indicative
;took the J-SPEC out, because I wanted to get rid of PCI-ADJ

; (ERB 2001-11-12) Experimenting with making te form TENSE tensed
; instead of TENSE te.

te-end := v-te-end-lex &
           [ORTH <! "て" !>,
	    SYNSEM.LOCAL.CAT.HEAD.FORMAL.AHON -,
            LMORPH-BIND-TYPE t-morph].

; (MS 2002-05-08) The comma seems to work similar to the TE ending in the conjunctive domain.
; (MS & FCB 2003-10-29) extended that to i-or-t-morph to cope for: 
; 走り 、 寝る as well as,  美しく 、 良い
; At present JACY doesn'_t accept either of these without the comma
; maybe it should, although that would greatly increase ambiguity

comma-te-end := comma-coord-end-lex &
           [ORTH <! "、" !>,
	    LMORPH-BIND-TYPE i-or-t-morph].


; (ERB 2001-11-12) Experimenting with making te form TENSE tensed
; instead of TENSE te.

de-end := v-te-end-lex &
           [ORTH <! "で" !>,
	    SYNSEM.LOCAL.CAT.HEAD.FORMAL.AHON -,
                  LMORPH-BIND-TYPE nd-morph].


;finite-present-polite-negative

masen-end :=  v-neg-polite-present-end-lex &
              [ORTH <! "ませ","ん" !>,
	       SYNSEM.LKEYS.KEYREL.PRED '_neg_v_rel,
	       LMORPH-BIND-TYPE i-morph].

masendeshita-end :=  v-neg-polite-past-end-lex &
              [ORTH <! "ませ","ん","でし","た" !>,
	       SYNSEM.LKEYS.KEYREL.PRED '_neg_v_rel,
	       LMORPH-BIND-TYPE i-morph].

arimasendeshita-end :=  v-neg-polite-past-end-lex &
              [ORTH <! "あり","ませ", "ん","でし","た" !>,
	       SYNSEM.LKEYS.KEYREL.PRED '_neg_v_rel,
               SYNSEM.LOCAL.CAT.VAL.SPR.FIRST.LOCAL.CAT.HEAD.KEYS.KEY tai_rel,
               LMORPH-BIND-TYPE a-morph].

arimasendeshita-adj-end :=  v-neg-polite-past-end-lex &
              [ORTH <! "あり","ませ", "ん","でし","た" !>,
	       SYNSEM.LKEYS.KEYREL.PRED '_neg_v_rel,
               SYNSEM.LOCAL.CAT.VAL.SPR.FIRST.LOCAL.CAT.HEAD i-adj-stem_head,
               LMORPH-BIND-TYPE a-morph].


;finite-present-plain-negative

nai-end := v-finite-present-plain-negative-end-lex &
           [ORTH <! "ない" !>,
	    INFLECTED +,
	    SYNSEM.LKEYS.KEYREL.PRED '_neg_v_rel]. 

;判らなくなりました

nai-notyetend  := v-noncaus-op-end-lex &
           [ORTH <! "ない" !>,
	    J-NEEDS-AFFIX +,
	    INFLECTED -,
	    LMORPH-BIND-TYPE a-morph,
	    STEMTYPE adj-stem,
	    SYNSEM [LOCAL [CAT [HEAD v-op-end_head & [MARK.FIRST [MODIFIED.PERIPH -,
							    LOCAL.CAT.HEAD verbend_head],
						      J-SPEC null,
						      COP-ARG -],
			       VAL.SPR.FIRST.LOCAL [CONT [MOD-HAND #modhd,
							  HOOK.LTOP #v-hd,
							  MOD-IND #event]]],
			  CONT [RELS <! #key & [ARG1 #neg-arg,
						 LBL #top] !>,
				HOOK [LTOP #top,
				      INDEX #event & [E.MOOD indicative]],
				MOD-HAND #modhd,
				MOD-IND #event,
				HCONS <! qeq & [LARG #v-hd,
						 HARG #neg-arg] !>]],
		    LKEYS.KEYREL #key & neg-relation & [PRED '_neg_v_rel],
		    MODIFIED.PERIPH +]].



hajimeru-end := v-noncaus-op-end-lex &
           [ORTH <! "はじめる" !>,
	    J-NEEDS-AFFIX +,
	    INFLECTED -,
	    LMORPH-BIND-TYPE i-morph,
	    STEMTYPE v-stem,
	    SYNSEM [LOCAL [CAT [HEAD v-op-end_head & [MARK < [MODIFIED.PERIPH -,
							    LOCAL.CAT.HEAD verbend_head] >,
						      COP-ARG -],
			       VAL.SPR.FIRST [LOCAL.CONT [MOD-HAND #modhd,
							  HOOK.LTOP #v-hd,
							  MOD-IND #event],
					      LKEYS.KEYREL.ARG1 #arg1]],
			  CONT [RELS <! #key & [ARG4 #arg4,
						 ARG1 #arg1,
						 LBL #top],
				       message & [PRED proposition_m_rel,
						  LBL #arg4,
						  MARG #subprpstn_soa]!>,
				HOOK [LTOP #top,
				      INDEX #event & [E.MOOD indicative]],
				MOD-HAND #modhd,
				MOD-IND #event,
				HCONS <! qeq & [LARG #v-hd,
						 HARG #subprpstn_soa] !>]],
		    LKEYS.KEYREL #key &  [PRED '_hajimeru_v_rel],
		    MODIFIED.PERIPH +]].

arimasen-tai-end := v-neg-end-lex &
           [ORTH <! "あり","ませ","ん" !>,
            J-NEEDS-AFFIX -,
           SYNSEM.LOCAL [CAT [HEAD [H-TENSE present,
                                    MOD < [LOCAL [CAT.HEAD noun_head,
                                                BAR +]] >,
                                    MARK null,
                                    C-MOD null,
                                    J-SPEC < [LOCAL.CAT.HEAD noun_head] >,
                                    FIN +,
                                    J-POSTHEAD rels,
                                    FORMAL.AHON +,
                                   MODUS negative],
                              VAL.SPR.FIRST.LKEYS.KEYREL tai-relation],
                         CONT.HOOK.INDEX.E.MOOD indicative],
            LMORPH-BIND-TYPE a-morph]. 

arimasen-adj-end := v-neg-end-lex &
           [ORTH <! "あり","ませ","ん" !>,
            J-NEEDS-AFFIX -,
           SYNSEM [LOCAL [CAT [HEAD [H-TENSE present,
                                    MOD < [LOCAL [CAT.HEAD noun_head,
                                                BAR +]] >,
                                    MARK null,
                                    C-MOD null,
                                    J-SPEC < [LOCAL.CAT.HEAD noun_head] >,
                                    FIN +,
                                    J-POSTHEAD rels,
                                    FORMAL.AHON +,
                                   MODUS negative],
                              VAL.SPR.FIRST.LOCAL.CAT.HEAD i-adj-stem_head],
			  CONT.HOOK.INDEX.E.MOOD indicative],
		   LKEYS.KEYREL.PRED '_neg_v_rel],
            LMORPH-BIND-TYPE a-morph]. 

nasa-end := v-neg-end-lex &
           [ORTH <! "な","さ" !>,
            J-NEEDS-AFFIX bool,
           SYNSEM [LOCAL [CAT [HEAD [H-TENSE present,
                                    MOD null,
                                    MARK.FIRST.LOCAL.CAT.HEAD na-adj_head,
                                    C-MOD null,
                                    J-SPEC null,
                                    FIN -]],
                         CONT.HOOK.INDEX.E.MOOD indicative],
		   LKEYS.KEYREL.PRED '_neg_v_rel],
            LMORPH-BIND-TYPE a-morph,
            RMORPH-BIND-TYPE shortadj-morph]. 

;;; (TT+all 2003-08-12) 
;; We think this should be a coordinator, more like the current entry for zuni
;; and it can'_t premodify nouns (unlike nu)!  


zu-end := v-neg-end-lex &
           [ORTH <! "ず" !>,
            J-NEEDS-AFFIX -,
           SYNSEM [LOCAL [CAT [HEAD [FIN -,
				    H-TENSE te,
                                    MOD null,
                                    MARK null,
                                    COP-ARG -,
                                    C-MOD.FIRST.LOCAL [CAT [HEAD verb_head & [MAIN-PRD +]],
                                               BAR +],
                                    J-POSTHEAD coord,
                                    FORMAL.AHON -]],
                         CONT.HOOK.INDEX.E.MOOD indicative],
		   LKEYS.KEYREL.PRED '_neg_v_rel],
            LMORPH-BIND-TYPE aa-morph]. 

nu-end := v-neg-end-lex &
           [ORTH <! "ぬ" !>,
            J-NEEDS-AFFIX -,
           SYNSEM [LOCAL [CAT [HEAD [H-TENSE present,
                                    MOD < [LOCAL [CAT.HEAD noun_head,
                                                BAR +]] >,
                                    MARK null,
                                    J-POSTHEAD rels,
                                    COP-ARG -,
                                   C-MOD null,
                                    J-SPEC < [LOCAL.CAT.HEAD noun_head] >,
                                    FIN +,
                                    FORMAL.AHON -]],
                         CONT.HOOK.INDEX.E.MOOD indicative],
		   LKEYS.KEYREL.PRED '_neg_v_rel],
            LMORPH-BIND-TYPE a-morph]. 

;te-plain-negative

; (ERB 2001-11-12) Experimenting with making te form TENSE tensed
; instead of TENSE te.


; (ERB 2001-11-12) Experimenting with making te form TENSE tensed
; instead of TENSE te.

; (MS 2004-11-09) UNSAT - on the SPR is too strong: 研究しないでいる

naide-end := v-neg-end-lex &
           [ORTH <! "ない" , "で" !>,
            J-NEEDS-AFFIX -,
            SYNSEM [LOCAL [CAT [HEAD [FIN -,
                                    H-TENSE tensed,
                                    J-SPEC < [LOCAL [CAT.HEAD verb_head &
                                                          [AUX aux_plus],
                                                BAR +],
					      LEX +] >,
                                    MARK null,
                                    COP-ARG -,
                                    MOD < [LOCAL [CAT [HEAD verb_head & [MAIN-PRD +]],
                                               BAR +]] >,
                                    J-POSTHEAD coord,
                                    FORMAL.AHON -],
			       VAL.SPR.FIRST.LOCAL.CAT.VAL.UNSAT bool],
                          CONT.HOOK.INDEX.E.MOOD indicative],
		   LKEYS.KEYREL.PRED '_neg_v_rel],
            LMORPH-BIND-TYPE a-morph].

; (ERB 2002-01-22) This doesn'_t want to undergo any of the ordinary
; head-adjunct rules and is too promiscuous in the sentence-te-coord-rule
; because its C-MOD is underspecified.  Try making C-MOD what the
; MOD value was and MOD none.  The semantic result is okay:  Get 
; coordination of negated first clause and second clause.  I guess
; that'_s pretty close to "without S1, S2".

zuni-end := v-neg-end-lex &
           [ORTH <! "ず" , "に" !>,
            J-NEEDS-AFFIX -,
            SYNSEM [LOCAL [CAT.HEAD [FIN -,
                                    H-TENSE te,
                                    J-SPEC null,
                                    MARK null,
                                    COP-ARG -,
                                    C-MOD.FIRST.LOCAL [CAT [HEAD verb_head & [MAIN-PRD +]],
                                               BAR +],
                                    MOD null,
                                    J-POSTHEAD coord,
                                    FORMAL.AHON -],
                          CONT.HOOK.INDEX.E.MOOD indicative],
		   LKEYS.KEYREL.PRED '_neg_v_rel],
            LMORPH-BIND-TYPE a-morph].

;negative (+ta)


;nakatt-end := v-neg-end-lex &
;               [ORTH <! "なかっ" !>,
;                J-NEEDS-AFFIX +,
;                SYNSEM.LOCAL [CAT.HEAD.MARK.FIRST.LOCAL.CAT.HEAD v-end_head,
;                              CONT.HOOK.INDEX.E.MOOD indicative],
;                LMORPH-BIND-TYPE a-morph].



;nonfinite-past-polite-conditional

mashitara-end  :=  v-cond-end-lex &
                   [ORTH <! "まし", "たら" !>,
                    SYNSEM.LOCAL [CAT [HEAD [FORMAL.AHON +,
                                             COP-ARG -],
                                      VAL.SPR.FIRST.LOCAL.CONT.HOOK.INDEX.E.TENSE past],
                                  CONT.HOOK.INDEX.E.MOOD indicative],
                     J-NEEDS-AFFIX -,
                    LMORPH-BIND-TYPE i-morph].



;nonfinite-past-polite-conditional

mashitaraba-end  :=  v-cond-end-lex &
                   [ORTH <! "まし", "たら" , "ば" !>,
                    SYNSEM.LOCAL.CAT [HEAD.FORMAL.AHON +,
                                      VAL.SPR.FIRST.LOCAL.CONT.HOOK.INDEX.E.TENSE past],
                     J-NEEDS-AFFIX -,
                    LMORPH-BIND-TYPE i-morph].


;nonfinite-past-plain-conditional

tara-end := v-cond-end-lex &
           [ORTH <! "たら" !>,
            SYNSEM.LOCAL.CAT [HEAD.FORMAL.AHON -,
                              VAL.SPR.FIRST.LOCAL.CONT.HOOK.INDEX.E.TENSE past],
            LMORPH-BIND-TYPE t-morph,
            J-NEEDS-AFFIX -].

tara-chasen-end := v-cond-end-lex &
           [ORTH <! "た","ら" !>,
            SYNSEM.LOCAL.CAT [HEAD.FORMAL.AHON -,
                              VAL.SPR.FIRST.LOCAL.CONT.HOOK.INDEX.E.TENSE past],
            LMORPH-BIND-TYPE t-morph,
            J-NEEDS-AFFIX -].


dara-end := v-cond-end-lex &
           [ORTH <! "だら" !>,
            SYNSEM.LOCAL.CAT [HEAD.FORMAL.AHON -,
                              VAL.SPR.FIRST.LOCAL.CONT.HOOK.INDEX.E.TENSE past],
            LMORPH-BIND-TYPE nd-morph,
            J-NEEDS-AFFIX -].

;nonfinite-past-polite-conditional

taraba-end := v-cond-end-lex &
           [ORTH <! "たら", "ば" !>,
            J-NEEDS-AFFIX -,
            SYNSEM.LOCAL.CAT [HEAD.FORMAL.AHON +,
                              VAL.SPR.FIRST.LOCAL.CONT.HOOK.INDEX.E.TENSE past],
            LMORPH-BIND-TYPE t-morph].

daraba-end := v-cond-end-lex &
           [ORTH <! "だら", "ば" !>,
            J-NEEDS-AFFIX -,
            SYNSEM.LOCAL.CAT [HEAD.FORMAL.AHON +,
                              VAL.SPR.FIRST.LOCAL.CONT.HOOK.INDEX.E.TENSE past],
            LMORPH-BIND-TYPE nd-morph].

;nonfinite-present-plain-conditional

ba-end := v-cond-end-lex &
           [ORTH <! "ば" !>,
            J-NEEDS-AFFIX -,
            LMORPH-BIND-TYPE cond-morph].



;nonfinite-present-plain-conditional

nagara-end := v-cond-end-lex &
           [ORTH <! "ながら" !>,
            J-NEEDS-AFFIX -,
            SYNSEM.LOCAL.CAT.VAL.SPR.FIRST.LOCAL.CONT.HOOK.INDEX.E.TENSE present,
            LMORPH-BIND-TYPE i-morph].



;finite-future-polite-consultative

mashou-end  :=  v-consultative-tense-end-lex &
                 [ORTH <! "ましょ", "う" !>,
                  SYNSEM.LOCAL.CAT.HEAD.FORMAL.AHON +,
                  LMORPH-BIND-TYPE i-morph].

;finite-future-plain-consultative: 食べ させ よう

you-end  :=  v-consultative-tense-end-lex &
                 [ORTH <! "よう" !>,
                  SYNSEM.LOCAL.CAT.HEAD.FORMAL.AHON -,
                  LMORPH-BIND-TYPE i-morph].



;finite-future-plain-consultative

u-end  := v-consultative-tense-end-lex &
           [ORTH <! "う" !>,
            SYNSEM.LOCAL.CAT.HEAD.FORMAL.AHON -,
	    LMORPH-BIND-TYPE o-morph].

;nonfinite-present-plain-negative-conditional


;nakere-end := v-neg-end-lex &
;           [ORTH <! "なけれ" !>,
;            J-NEEDS-AFFIX +,
;            INFLECTED +,
;            SYNSEM.LOCAL.CAT.HEAD [H-TENSE present,
;                                   MODUS negative,
;                                   MARK.FIRST.LOCAL.CAT.HEAD verbend_head,
;                                   J-SPEC null],
;            LMORPH-BIND-TYPE a-morph].



;nonfinite-present-plain-potential-conditional


;rere-end  := v-simple-end-lex &
;           [ORTH <! "れれ" !>,
;            J-NEEDS-AFFIX +,
;            SYNSEM.LOCAL [CAT [HEAD verb_head & [FORMAL.AHON -,
;                                                 MARK.FIRST.LOCAL.CAT.HEAD v-end_head],
;                               VAL.SPR.FIRSTLKEYS.KEYREL.ARG1 #arg1],
;                          CONT [RELS <! [PRED '_rareru_v_rel, 
;                                          ARG0 #ind,
;                                          ARG1 #arg1,
;                                          SCOPE #hole,
;                                          LBL #hand] !>,
;                                HCONS.LIST < [LARG #hole, HARG #hand], ... >,
;                                LBL #hand,
;                                HOOK.INDEX #ind]],
;                  LMORPH-BIND-TYPE a-morph].


;tai 


tai-end := tai-end-lex &
           [ORTH <! "たい" !>,
	    SYNSEM.LOCAL [CAT.HEAD [FORMAL.SHON -,
				    C-MOD null]],
            LMORPH-BIND-TYPE i-morph].

tagaru-multiword-notyetend := tai-notyetend-lex &
           [ORTH <! "た","がる" !>,
            STEMTYPE c-stem].

tagaru-notyetend := tai-notyetend-lex &
           [ORTH <! "たがる" !>,
            STEMTYPE c-stem].


tagaru-end := tai-end-lex &
           [ORTH <! "たがる" !>,
	    SYNSEM.LOCAL.CAT.HEAD.C-MOD null,
            LMORPH-BIND-TYPE i-morph].

tagaru-multi-end := tai-end-lex &
           [ORTH <! "た","がる" !>,
	    SYNSEM.LOCAL.CAT.HEAD.C-MOD null,
            LMORPH-BIND-TYPE i-morph].

taku-end := tai-end-lex-basic &
           [ORTH <! "たく" !>,
            J-NEEDS-AFFIX -,
            INFLECTED +,
            SYNSEM.LOCAL [CAT [HEAD [C-MOD < [LOCAL [CAT [HEAD verb_head & [MAIN-PRD +]],
                                                BAR +]] >, 
                                     H-TENSE te,
                                     J-POSTHEAD coord,
                                     MARK null,
                                     COP-ARG -,
                                    J-SPEC < [LOCAL [CAT.HEAD verb_head & [AUX aux_plus],
                                                 BAR +],
					      LEX +] >,
                                     FIN -,
                                     FORMAL [SHON -],
                                     MODUS voluntative]]],
            LMORPH-BIND-TYPE i-morph].

tai-notyetend := tai-notyetend-lex &
           [ORTH <! "たい" !>,
            STEMTYPE adj-stem].


            



;finite-present-plain-causative

seru-intrans-end := caus-intrans-end-lex &
           [ORTH <! "せる" !>,
            INFLECTED -,
            SYNSEM [LOCAL [CAT.HEAD [MOD < [LOCAL [CAT.HEAD noun_head,
                                                 BAR +]] >,
                                     C-MOD null,
                                     J-POSTHEAD rels,
                                     J-SPEC < [LOCAL.CAT.HEAD noun_head] >]],
                    MODIFIED.PERIPH bool],
            LMORPH-BIND-TYPE pass-c-stem-morph,
            STEMTYPE v-stem].

;saseru only with v-stem.

saseru-intrans-end := caus-intrans-end-lex &
           [ORTH <! "させる" !>,
            INFLECTED -,
            SYNSEM [LOCAL [CAT.HEAD [MOD < [LOCAL [CAT.HEAD noun_head,
                                                 BAR +]] >,
                                     C-MOD null,
                                     J-SPEC < [LOCAL.CAT.HEAD noun_head] >,
                                     J-POSTHEAD rels]],
                    MODIFIED.PERIPH +],
            LMORPH-BIND-TYPE pass-v-stem-morph,
            STEMTYPE v-stem].


; (TT 2003-09-03) lex type を *-obj-* と *-obj2-* に

seru-trans-obj-end := caus-trans-obj-end-lex &
           [ORTH <! "せる" !>,
            INFLECTED -,
            SYNSEM [LOCAL [CAT.HEAD [MOD < [LOCAL [CAT.HEAD noun_head,
                                                 BAR +]] >,
                                     C-MOD null,
                                     J-POSTHEAD rels]],
                    MODIFIED.PERIPH +],
            LMORPH-BIND-TYPE pass-c-stem-morph,
            STEMTYPE v-stem].

seru-trans-obj2-end := caus-trans-obj2-end-lex &
           [ORTH <! "せる" !>,
            INFLECTED -,
            SYNSEM [LOCAL [CAT.HEAD [MOD < [LOCAL [CAT.HEAD noun_head,
                                                 BAR +]] >,
                                     C-MOD null,
                                     J-POSTHEAD rels]],
                    MODIFIED.PERIPH +],
            LMORPH-BIND-TYPE pass-c-stem-morph,
            STEMTYPE v-stem].

; (TT 2003-09-03) lex type を *-obj-* と *-obj2-* に

saseru-trans-obj-end := caus-trans-obj-end-lex &
           [ORTH <! "させる" !>,
            INFLECTED -,
            SYNSEM [LOCAL [CAT.HEAD [MOD < [LOCAL [CAT.HEAD noun_head,
                                                 BAR +]] >,
                                     C-MOD null,
                                     J-POSTHEAD rels]],
                    MODIFIED.PERIPH +],
            LMORPH-BIND-TYPE pass-v-stem-morph,
            STEMTYPE v-stem].

saseru-trans-obj2-end := caus-trans-obj2-end-lex &
           [ORTH <! "させる" !>,
            INFLECTED -,
            SYNSEM [LOCAL [CAT.HEAD [MOD < [LOCAL [CAT.HEAD noun_head,
                                                 BAR +]] >,
                                     C-MOD null,
                                     J-POSTHEAD rels]],
                    MODIFIED.PERIPH +],
            LMORPH-BIND-TYPE pass-v-stem-morph,
            STEMTYPE v-stem].

; (SF 2003-09-02) CP from させる

sasu-intrans-end := caus-intrans-end-lex &
           [ORTH <! "さす" !>,
            INFLECTED -,
            SYNSEM [LOCAL [CAT.HEAD [MOD < [LOCAL [CAT.HEAD noun_head,
                                                 BAR +]] >,
                                     C-MOD null,
                                     J-SPEC < [LOCAL.CAT.HEAD noun_head] >,
                                     J-POSTHEAD rels]],
                    MODIFIED.PERIPH +],
            LMORPH-BIND-TYPE pass-v-stem-morph,
            STEMTYPE v-stem].

; (SF 2003-09-02 CP) from させる
; (TT 2003-09-03) lex type を *-obj-* と *-obj2-* に

sasu-trans-obj-end := caus-trans-obj-end-lex &
           [ORTH <! "さす" !>,
            INFLECTED -,
            SYNSEM [LOCAL [CAT.HEAD [MOD < [LOCAL [CAT.HEAD noun_head,
                                                 BAR +]] >,
                                     C-MOD null,
                                     J-POSTHEAD rels]],
                    MODIFIED.PERIPH +],
            LMORPH-BIND-TYPE pass-v-stem-morph,
            STEMTYPE v-stem].

sasu-trans-obj2-end := caus-trans-obj2-end-lex &
           [ORTH <! "さす" !>,
            INFLECTED -,
            SYNSEM [LOCAL [CAT.HEAD [MOD < [LOCAL [CAT.HEAD noun_head,
                                                 BAR +]] >,
                                     C-MOD null,
                                     J-POSTHEAD rels]],
                    MODIFIED.PERIPH +],
            LMORPH-BIND-TYPE pass-v-stem-morph,
            STEMTYPE v-stem].

;passive

reru-simple-pass-end := simple-pass-end-lex &
           [ORTH <! "れる" !>,
            INFLECTED -,
            SYNSEM [LOCAL.CAT.HEAD [MODUS passive]],
            LMORPH-BIND-TYPE pass-c-stem-morph,
            STEMTYPE v-stem].


rareru-simple-pass-end := simple-pass-end-lex &
           [ORTH <! "られる" !>,
            INFLECTED -,
            SYNSEM.LOCAL [CAT [HEAD [MOD < [LOCAL [CAT.HEAD noun_head,
                                                BAR +]] >,
                                    J-SPEC < [LOCAL.CAT.HEAD noun_head] >,
                                    FIN +,
                                    J-POSTHEAD rels,
                                    C-MOD null,
                                    MODUS passive,
                                    FORMAL.AHON -]]],
            LMORPH-BIND-TYPE pass-v-stem-morph,
            STEMTYPE v-stem].

reru-ditrans-pass-end := ditrans-pass-end-lex &
           [ORTH <! "れる" !>,
            INFLECTED -,
            SYNSEM.LOCAL [CAT.HEAD [MODUS passive]],
            LMORPH-BIND-TYPE pass-c-stem-morph,
            STEMTYPE v-stem].

rareru-ditrans-pass-end := ditrans-pass-end-lex &
           [ORTH <! "られる" !>,
            INFLECTED -,
            SYNSEM.LOCAL [CAT.HEAD [MODUS passive]],
            LMORPH-BIND-TYPE pass-v-stem-morph,
            STEMTYPE v-stem].

reru-intrans-adv-pass-end := adversative-intrans-pass-end-lex &
           [ORTH <! "れる" !>,
            INFLECTED -,
            SYNSEM.LOCAL [CAT.HEAD [MOD < [LOCAL [CAT.HEAD noun_head,
                                                BAR +]] >,
                                    J-POSTHEAD rels,
                                    J-SPEC < [LOCAL.CAT.HEAD noun_head] >,
                                    FIN +,
                                    C-MOD null,
                                    MODUS passive,
                                    FORMAL.AHON -]],
            LMORPH-BIND-TYPE pass-c-stem-morph,
            STEMTYPE v-stem].


rareru-intrans-adv-pass-end := adversative-intrans-pass-end-lex &
           [ORTH <! "られる" !>,
            INFLECTED -,
            SYNSEM.LOCAL [CAT [HEAD [MOD < [LOCAL [CAT.HEAD noun_head,
                                                BAR +]] >,
                                    J-SPEC < [LOCAL.CAT.HEAD noun_head] >,
                                    FIN +,
                                    J-POSTHEAD rels,
                                    C-MOD null,
                                    MODUS passive,
                                    FORMAL.AHON -]]],
            LMORPH-BIND-TYPE pass-v-stem-morph,
            STEMTYPE v-stem].

reru-trans-adv-pass-end := adversative-trans-pass-end-lex &
           [ORTH <! "れる" !>,
            INFLECTED -,
            SYNSEM.LOCAL [CAT.HEAD [MOD < [LOCAL [CAT.HEAD noun_head,
                                                BAR +]] >,
                                    J-POSTHEAD rels,
                                    J-SPEC < [LOCAL.CAT.HEAD noun_head] >,
                                    FIN +,
                                    C-MOD null,
                                    MODUS passive,
                                    FORMAL.AHON -]],
            LMORPH-BIND-TYPE pass-c-stem-morph,
            STEMTYPE v-stem].


rareru-trans-adv-pass-end := adversative-trans-pass-end-lex &
           [ORTH <! "られる" !>,
            INFLECTED -,
            SYNSEM.LOCAL [CAT [HEAD [MOD < [LOCAL [CAT.HEAD noun_head,
                                                BAR +]] >,
                                    J-SPEC < [LOCAL.CAT.HEAD noun_head] >,
                                    FIN +,
                                    J-POSTHEAD rels,
                                    C-MOD null,
                                    MODUS passive,
                                    FORMAL.AHON -]]],
            LMORPH-BIND-TYPE pass-v-stem-morph,
            STEMTYPE v-stem].


reru-honorific-pass-end := v-honorific-end-lex &
                           [ORTH <! "れる" !>,
                            INFLECTED -,
                            SYNSEM.LOCAL [CAT.HEAD [MOD < [LOCAL [CAT.HEAD noun_head,
								  BAR +]] >,
                                                    J-POSTHEAD rels,
                                                    J-SPEC < [LOCAL.CAT.HEAD noun_head] >,
                                                    FIN +,
                                                    C-MOD null,
                                                    MODUS indicative,
                                                    FORMAL.AHON -]],
                            LMORPH-BIND-TYPE pass-c-stem-morph,
            STEMTYPE v-stem].

rareru-honorific-pass-end := v-honorific-end-lex &
                           [ORTH <! "られる" !>,
                            INFLECTED -,
            SYNSEM.LOCAL [CAT.HEAD [MOD < [LOCAL [CAT.HEAD noun_head,
                                                BAR +]] >,
                                    J-POSTHEAD rels,
                                    J-SPEC < [LOCAL.CAT.HEAD noun_head] >,
                                    FIN +,
                                    C-MOD null,
                                    MODUS indicative,
                                    FORMAL.AHON -]],
            LMORPH-BIND-TYPE pass-v-stem-morph,
            STEMTYPE v-stem].




rareru-pot-end := rareru-end-lex &
                  [ORTH <! "られる" !>,
                   SYNSEM.LOCAL.CAT.HEAD.MODUS potential,
                   INFLECTED -,
                   LMORPH-BIND-TYPE i-morph,
                   STEMTYPE v-stem].


ru-pot-end := rareru-end-lex &
                  [ORTH <! "る" !>,
                   SYNSEM.LOCAL.CAT.HEAD.MODUS potential,
                   INFLECTED -,
                   LMORPH-BIND-TYPE cond-morph].

reru-pot-end := rareru-end-lex &
                  [ORTH <! "れる" !>,
                   SYNSEM.LOCAL.CAT.HEAD.MODUS potential,
                   INFLECTED -,
                   LMORPH-BIND-TYPE cond-exceptional-morph].


sou-varg-end := na-varg-end-lex &
           [ORTH <! "そう" !>,
            SYNSEM.LKEYS.KEYREL.PRED '_sou_v_rel].

sasou-end := na-varg-end-lex &
           [ORTH <! "さ","そう" !>,
            SYNSEM [LOCAL.CAT.VAL.SPR.FIRST.LOCAL.CAT.HEAD.MODUS negative,
		    LKEYS.KEYREL.PRED '_sou_v_rel]].

you-varg-end := na-varg-end-lex &
           [ORTH <! "よう" !>,
            SYNSEM.LKEYS.KEYREL.PRED '_you_v_rel].

you-kanji-varg-end := na-varg-end-lex &
           [ORTH <! "様" !>,
            SYNSEM.LKEYS.KEYREL.PRED '_you_v_rel].

toori-varg-end := na-varg-end-lex &
           [ORTH <! "とおり" !>,
            SYNSEM.LKEYS.KEYREL.PRED '_toori_v_rel].

toori-kanji-varg-end := na-varg-end-lex &
           [ORTH <! "通り" !>,
            SYNSEM.LKEYS.KEYREL.PRED '_toori_v_rel].

sou-naarg-end := na-naarg-end-lex &
           [ORTH <! "そう" !>,
            SYNSEM.LKEYS.KEYREL.PRED '_sou_v_rel].




;do not want to

mai-end  :=  mai-end-lex &
                 [ORTH <! "まい" !>,
                  J-NEEDS-AFFIX -,
                  SYNSEM.LOCAL [CAT.HEAD [MODUS indicative,
                                          MOD null,
                                          C-MOD null,
                                          H-TENSE present,
                                          MARK null,
                                          J-SPEC null,
                                          FIN +]],
                  LMORPH-BIND-TYPE mai-morph].

;tend to

gachi-end := v2na-end-lex &
                 [ORTH <! "がち" !>,
                  SYNSEM.LKEYS.KEYREL.PRED '_gachi_v_rel].

;tend to (visible indication of tendency)

gimi-end := v2na-end-lex &
                 [ORTH <! "ぎみ" !>,
                  SYNSEM.LKEYS.KEYREL.PRED '_gimi_v_rel].

;imperative

nasai-end  := v-imperative-tense-end-lex &
                 [ORTH <! "なさい" !>,
                  LMORPH-BIND-TYPE i-morph].

nasai-chasen-end  := v-imperative-tense-end-lex &
                 [ORTH <! "な","さ","い" !>,
                  LMORPH-BIND-TYPE i-morph].



;imperative on VNs (VN + ください)

kudasai-vn-end-1 := kudasai-vn-end &
                    [ORTH <! "ください" !>].

kudasai-vn-end-2 := kudasai-vn-end &
                    [ORTH <! "下さい" !>].

;easy to

yasui-v1-end := v2adj-end-v1-nmod-lex &
             [ORTH <! "やすい" !>,
              SYNSEM [LKEYS.KEYREL.PRED '_yasui_v_rel]].

yasui-v1-notyetend :=  v2adj-notyetend-lex-v1  &
             [ORTH <! "やす" !>,
              SYNSEM.LKEYS.KEYREL.PRED '_yasui_v_rel].

yasui-v2-end := v2adj-end-v2-nmod-lex  &
             [ORTH <! "やすい" !>,
              SYNSEM.LKEYS.KEYREL.PRED '_yasui_v_rel].

yasui-v2-notyetend := v2adj-notyetend-lex-v2 &
             [ORTH <! "やすい" !>,
              SYNSEM [LKEYS.KEYREL.PRED '_yasui_v_rel]].

nikui-v1-end := v2adj-end-v1-nmod-lex &
             [ORTH <! "にくい" !>,
              SYNSEM.LKEYS.KEYREL.PRED '_nikui_v_rel].


nikui-v1-notyetend := v2adj-notyetend-lex-v1 &
             [ORTH <! "にくい" !>,
              SYNSEM [LKEYS.KEYREL.PRED '_nikui_v_rel,
		      MODIFIED.PERIPH +]].

nikui-v2-end := v2adj-nmod-maybeend-lex-v2 &
             [ORTH <! "にくい" !>,
              SYNSEM.LKEYS.KEYREL.PRED '_nikui_v_rel].


nikui-v2-notyetend := v2adj-notyetend-lex-v2 &
             [ORTH <! "にくい" !>,
             SYNSEM [LKEYS.KEYREL.PRED '_nikui_v_rel]].

;hard to


gatai-v1-end := v2adj-end-v1-nmod-lex &
             [ORTH <! "難い" !>,
              SYNSEM.LKEYS.KEYREL.PRED '_gatai_v_rel].

; (SF 2003-08-25) same as 難い
; (MS 2003-11-25) adapted

gatai-hira-v1-end := v2adj-end-v2-nmod-lex &
             [ORTH <! "がたい" !>,
              SYNSEM.LKEYS.KEYREL.PRED '_gatai_v_rel].


gatai-v1-notyetend := v2adj-notyetend-lex-v1 &
             [ORTH <! "難い" !>,
              SYNSEM [LKEYS.KEYREL.PRED '_gatai_v_rel,
		      MODIFIED.PERIPH +]].

gatai-v2-end := v2adj-end-v2-nmod-lex &
             [ORTH <! "難い" !>,
              SYNSEM.LKEYS.KEYREL.PRED '_gatai_v_rel].


gatai-v2-notyetend := v2adj-notyetend-lex-v2 &
             [ORTH <! "難い" !>,
              SYNSEM [LKEYS.KEYREL.PRED '_gatai_v_rel]].


;"食べられるだけ食べたい"

dake-v2adv-end := v2adv-end-lex &
		  [ORTH <! "だけ" !>,
		   SYNSEM [LKEYS.KEYREL.PRED '_dake_v_rel,
			   LOCAL.CAT.HEAD.MOD < [LOCAL.CAT.HEAD.MODUS voluntative] >],
		   LMORPH-BIND-TYPE mai-morph].

;contracted forms
;te iru - teru

; (ERB 2001-12-05) Moving to pure aspect auxiliary analysis.

; (ERB 2001-12-05) This is combining with too many utterance rules.
; Since てた seems to be produced by some other means, try constraining
; H-TENSE and TENSE.  Also need to constrain MODUS to keep it out
; of the imperative utterance rule.
; (MS 2001-12-6) teru now undergoes the infl rule.


teru-end :=  v-contracted-aux-end-lex &
             [ORTH <! "てる" !>,
              J-NEEDS-AFFIX +,
              SYNSEM.LOCAL [CAT [HEAD [MODUS #modus],
                                 VAL.SPR.FIRST.LOCAL.CAT.HEAD.MODUS #modus],
                            CONT [HOOK.INDEX.E [ ASPECT progressive,
                                            MOOD #modus]]],
              LMORPH-BIND-TYPE t-morph].

;te shimau - chau

; (ERB 2001-12-05) Pure auxiliary analysis.

chau-end :=  v-contracted-aux-end-lex &
             [ORTH <! "ちゃう" !>,
              J-NEEDS-AFFIX -,
	      INFLECTED -,
              SYNSEM.LOCAL [CAT [HEAD [COP-ARG -,
				       MODUS #modus],
                                 VAL.SPR.FIRST.LOCAL.CAT.HEAD.MODUS #modus],
                            CONT [HOOK.INDEX.E [MOOD indicative,
                                           ASPECT terminative]]],
              LMORPH-BIND-TYPE t-morph].

jau-end :=  v-contracted-aux-end-lex &
             [ORTH <! "じゃう" !>,
              J-NEEDS-AFFIX -,
	      INFLECTED -,
	      SYNSEM.LOCAL [CAT [HEAD [COP-ARG -,
                                       MODUS #modus],
                                 VAL.SPR.FIRST.LOCAL.CAT.HEAD.MODUS #modus],
                            CONT [HOOK.INDEX.E [MOOD indicative,
                                           ASPECT terminative]]],
              LMORPH-BIND-TYPE nd-morph].

jau-chasen-end :=  v-contracted-aux-end-lex &
             [ORTH <! "じゃ","う" !>,
              J-NEEDS-AFFIX -,
	      INFLECTED -,
	      SYNSEM.LOCAL [CAT [HEAD [COP-ARG -,
                                       MODUS #modus],
                                 VAL.SPR.FIRST.LOCAL.CAT.HEAD.MODUS #modus],
                            CONT [HOOK.INDEX.E [MOOD indicative,
                                           ASPECT terminative]]],
              LMORPH-BIND-TYPE nd-morph].

ni-end := naadj2adv-end-lex &
          [ORTH <! "に" !>,
           J-NEEDS-AFFIX -,
           SYNSEM.LOCAL.CAT.HEAD.J-POSTHEAD left].


naru-end := v-noncaus-op-end-lex &
	    [ORTH <! "なる" !>,
	     INFLECTED -,
	     LMORPH-BIND-TYPE a-morph,
	     STEMTYPE c-stem,
	     SYNSEM [LOCAL [CAT [HEAD v-op-end_head & [MODUS indicative],
				 VAL.SPR.FIRST.LOCAL.CONT [HOOK [LTOP #arg-h,
								 INDEX #mod-i],
							     MOD-HAND #mod-h]],
			    CONT [HOOK [INDEX #event,
					LTOP #hd],
				  MOD-HAND #mod-h,
				  MOD-IND #mod-i,
				  RELS <! #key !>,
				  HCONS <! !>]],
		     LKEYS.KEYREL #key & adv-relation &
			   [PRED '_naru_v_rel,
			    LBL #hd,
			    ARG1 #arg-h,
			    ARG0 #event],
		     MODIFIED.PERIPH +]].



;have to

nakereba-narimasen-end  :=  v-neg-end-lex &
              [ORTH <! "なけれ","ば","なり","ませ", "ん" !>,
               J-NEEDS-AFFIX -,
               SYNSEM [LOCAL [CAT.HEAD [MODUS negative,
                                       MOD null,
                                       C-MOD null,
                                       H-TENSE present,
                                       MARK null,
                                       J-SPEC null,
                                       FIN +,
                                       FORMAL.AHON +],
                             CONT.HOOK.INDEX.E.MOOD indicative],
		       LKEYS.KEYREL.PRED '_have_v_to_rel],
               LMORPH-BIND-TYPE a-morph].	



;cannot help doing, have to

zaru-wo-emasen-end := v-neg-polite-present-end-lex  &
              [ORTH <! "ざる","を", "え", "ませ", "ん" !>,
               SYNSEM.LKEYS.KEYREL.PRED '_cant_v_help_rel,
	       LMORPH-BIND-TYPE a-morph].



garu-adj2v-end := adj2v-end-lex &
             [ORTH <! "がる" !>].

garu-na-end := naadj2v-end-lex &
	       [ORTH <! "がる" !>].

garu-v-end := garu-end-lex &
             [ORTH <! "がる" !>].

kata-v-end := v2n-end-lex &
	      [ORTH <! "方" !>,
	       SYNSEM.LKEYS.KEYREL.PRED '_kata_v_rel].
